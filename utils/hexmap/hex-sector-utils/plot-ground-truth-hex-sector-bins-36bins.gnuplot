# Gnuplot script to generate a histogram plot of flower data with flowers laid
# out in hex sector space in bins of 10 degrees, for all flower types present in a 
# simulation run at the specified generation number, using input data from the
# specified file.
#
# The input file is a 4-field CSV file as generated by the utility script
# gen-hex-sector-bin-means-stds
#
# Call like this, e.g.
# > gnuplot -c plot-hex-sector-bins.gnuplot filebase gennum titletext x0pos[L|Q|M] xshift[10|5] prefplot[none|bee|hoverfly|flat] W
# e.g.
# > gnuplot -c plot-hex-sector-bins.gnuplot hex-sector-bin-means-stds 0
#
# The script looks for an input data file named [filebase]-gen-N.csv
#
# The output is saved in a file named [filebase]-gen-N-hex-sector.png

beePrefFile="~/evobee-hexmap/giurfa-pref-by-theta.csv"
hoverflyPrefFile="~/evobee-hexmap/hoverfly-pref-by-theta.csv"
flatPrefFile="~/evobee-hexmap/flat-pref-by-theta.csv"

filebase="hex-sector-bin-means-stds"
gen="0"
titletext="Aus Dataset 1 (minus orchids) FULL DATASET"
zeropos="Q"
xshift=5.0
prefs="bee"
prefFile=beePrefFile
binwidth=10

infile="/home/tim/evobee/code/evobee/utils/hexmap/aus-data-20210111/aus-ex-orchid-and-no-dom-dataset-1-only-histogram-36-bins.csv"
outfile="/home/tim/evobee/code/evobee/utils/hexmap/aus-data-20210111/aus-ex-orchid-and-no-dom-dataset-1-only-histogram-36-bins.png"

set datafile separator ","
set size square
set title font ",13"
set title titletext
set xlabel font ",12"
set xlabel "Hexagon sector"
set ylabel font ",12"
set ylabel "Species count"
set xtics nomirror out 20
set xtics font ",11"
set ytics nomirror out
set y2tics
set y2label font ",12"
set y2label "Pollinator Innate Preference (normalised units)"
set boxwidth binwidth absolute
set style fill solid 1.0 border lc "gray30"
unset key
if (zeropos eq "M") {
    set xrange[-175:185]
} else {
    if (zeropos eq "Q") {
        set xrange[-85:275]
    } else {
        set xrange[0:365]
    }
}
set yrange[0:150]
set y2range [0:0.015]

set term png size 800,800
set output outfile

# the following simple RGB utility function is from 
# https://livebook.manning.com/book/gnuplot-in-action-second-edition/chapter-9/33
pack( r, g, b ) = 2**16*r + 2**8*g + b

if (zeropos eq "M") {
    plot infile using (($1*10)+xshift)-(int($1/18)*360):3:4 with boxerrorbars fillcolor "gray70"
} else {
    if (zeropos eq "Q") {
        if (prefs eq "none") {
            plot infile using (($1*10)+xshift)-(int($1/27)*360):3:4 with boxerrorbars fillcolor "gray70"
        } else {
            plot infile using (($1*10)+xshift)-(int($1/27)*360):3:4 with boxerrorbars fillcolor "gray80", prefFile using ($1-int($1/270)*360):6 axes x1y2 w lp lt 6
        }
    } else {
        plot infile using (($1*10)+int(xshift)):3:4 with boxerrorbars fillcolor "gray70"
    }
}
