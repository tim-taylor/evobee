filebase="binned-numflowers-across-runs-means-stds-w10"
gen="2499"

prefFile="~/evobee-hexmap/giurfa-pref-by-theta.csv"
xshift=5

fbaseIN=sprintf("%s-gen-%s", filebase, gen)
infile=sprintf("%s.csv", fbaseIN)
fbaseOUT=sprintf("%s-0q-gen-%04d", filebase, (gen+0))
outfile=sprintf("%s-CUMUL.png", fbaseOUT)


set datafile separator ","
set size square
set title font ",13"
set title "Aus Dataset 1 (minus orchids), Giurfa Prefs SWIP, GC on 0.10, LC on / Gen 2500"
set xlabel font ",12"
set xlabel "Hexagon sector"
set ylabel font ",12"
set ylabel "Plant count"
set xtics nomirror out 20
set xtics font ",11"
set ytics nomirror out
set y2tics
set y2label font ",12"
set y2label "Pollinator Innate Preference (normalised units)"
set boxwidth 10 absolute
set style fill solid 1.0 border lc "gray30"

#unset key
set key left top Left box

set xrange[-85:275]
set yrange[0:36500]
set y2range [0:0.015]

set term png size 800,800
set output outfile

plot infile using (($1*10)+xshift)-(int($1/27)*360):3:4 with boxerrorbars fillcolor "gray80" title "binned count", infile using (($1*10)+xshift)-(int($1/27)*360):3 axes x1y1 smooth cumul w lp lt 7 title "cumulative count", prefFile using ($1-int($1/270)*360):6 axes x1y2 w lp lt 6 title "pollinator preference"
